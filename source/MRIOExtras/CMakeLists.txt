cmake_minimum_required(VERSION 3.16 FATAL_ERROR)
set(CMAKE_CXX_STANDARD ${MR_CXX_STANDARD})
set(CMAKE_CXX_STANDARD_REQUIRED ON)

project(MRIOExtras CXX)

option(MRIOEXTRAS_NO_3MF "Disable 3MF support" OFF)
option(MRIOEXTRAS_NO_CTM "Disable OpenCTM support" OFF)
option(MRIOEXTRAS_NO_E57 "Disable E57 support" OFF)
option(MRIOEXTRAS_NO_GLTF "Disable glTF support" OFF)
option(MRIOEXTRAS_NO_JPEG "Disable JPEG support" OFF)
option(MRIOEXTRAS_NO_LAS "Disable LAS/LAZ support" OFF)
option(MRIOEXTRAS_NO_PDF "Disable PDF support" OFF)
option(MRIOEXTRAS_NO_PNG "Disable PNG support" OFF)
option(MRIOEXTRAS_NO_STEP "Disable STEP support" OFF)
option(MRIOEXTRAS_NO_ZLIB "Disable Zlib usage" OFF)

option(MRIOEXTRAS_OPENCASCADE_USE_XDE "Use OpenCASCADE XDE module" ON)

IF(MR_EMSCRIPTEN)
  set(MRIOEXTRAS_NO_PDF ON)
  set(MRIOEXTRAS_NO_STEP ON)
ENDIF()

file(GLOB SOURCES "*.cpp")
file(GLOB HEADERS "*.h")

add_library(${PROJECT_NAME} SHARED ${SOURCES} ${HEADERS})

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/config_cmake.h.in ${CMAKE_CURRENT_SOURCE_DIR}/config_cmake.h)

set(MRIOEXTRAS_DEPENDENCIES "")
set(MRIOEXTRAS_INCLUDE_DIRS "")
set(MRIOEXTRAS_LIBRARY_DIRS "")

IF(NOT MRIOEXTRAS_NO_3MF)
  list(APPEND MRIOEXTRAS_DEPENDENCIES tinyxml2)
ENDIF()

IF(NOT MRIOEXTRAS_NO_OPENCTM)
  list(APPEND MRIOEXTRAS_DEPENDENCIES OpenCTM)
ENDIF()

IF(NOT MRIOEXTRAS_NO_E57)
  list(APPEND MRIOEXTRAS_DEPENDENCIES E57Format)
ENDIF()

IF(NOT MRIOEXTRAS_NO_JPEG)
  IF(APPLE)
    target_include_directories(${PROJECT_NAME} PUBLIC $<BUILD_INTERFACE:${HOMEBREW_PREFIX}/opt/jpeg-turbo/include>) # @todo
    list(APPEND MRIOEXTRAS_DEPENDENCIES ${HOMEBREW_PREFIX}/opt/jpeg-turbo/lib/libturbojpeg.dylib)
  ELSE()
    list(APPEND MRIOEXTRAS_DEPENDENCIES turbojpeg)
  ENDIF()
ENDIF()

IF(NOT MRIOEXTRAS_NO_LAS)
  IF(MR_EMSCRIPTEN)
    list(APPEND MRIOEXTRAS_DEPENDENCIES lazperf_s)
  ELSE()
    list(APPEND MRIOEXTRAS_DEPENDENCIES lazperf)
  ENDIF()
ENDIF()

IF(NOT MRIOEXTRAS_NO_PDF)
  list(APPEND MRIOEXTRAS_DEPENDENCIES hpdf)
ENDIF()

IF(NOT MRIOEXTRAS_NO_PNG)
  IF(MR_EMSCRIPTEN)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -s USE_LIBPNG=1")
  ELSE()
    list(APPEND MRIOEXTRAS_DEPENDENCIES png)
  ENDIF()
ENDIF()

IF(NOT MRIOEXTRAS_NO_STEP)
  find_package(OpenCASCADE REQUIRED
    COMPONENTS
    # NOTE: the component order is significant
    FoundationClasses
    ModelingData
    ModelingAlgorithms
    Visualization
    ApplicationFramework
    DataExchange
  )
  set(OpenCASCADE_REQUIRED_LIBRARIES TKernel TKBRep TKMath TKMesh TKDESTEP TKSTEP TKXSBase)
  IF(MRIOEXTRAS_OPENCASCADE_USE_XDE)
    list(APPEND OpenCASCADE_REQUIRED_LIBRARIES TKXDESTEP TKLCAF TKXCAF TKTopAlgo)
  ENDIF()
  FOREACH(LIBRARY_NAME ${OpenCASCADE_LIBRARIES})
    IF(${LIBRARY_NAME} IN_LIST OpenCASCADE_REQUIRED_LIBRARIES)
      list(APPEND MRIOEXTRAS_DEPENDENCIES ${LIBRARY_NAME})
    ENDIF()
  ENDFOREACH()
  list(APPEND MRIOEXTRAS_INCLUDE_DIRS ${OpenCASCADE_INCLUDE_DIR})
  list(APPEND MRIOEXTRAS_LIBRARY_DIRS ${OpenCASCADE_LIBRARY_DIR})
ENDIF()

IF(NOT MRIOEXTRAS_NO_ZLIB)
  IF(MR_EMSCRIPTEN)
    list(APPEND MRIOEXTRAS_DEPENDENCIES z)
  ELSE()
    find_package(ZLIB REQUIRED)
    list(APPEND MRIOEXTRAS_DEPENDENCIES ZLIB::ZLIB)
  ENDIF()
ENDIF()

IF(BUILD_TESTING)
  IF(APPLE)
    list(APPEND MRIOEXTRAS_DEPENDENCIES GTest::gtest)
  ELSE()
    list(APPEND MRIOEXTRAS_DEPENDENCIES gtest)
  ENDIF()
ENDIF()

target_include_directories(${PROJECT_NAME} BEFORE PRIVATE
  ${MRIOEXTRAS_INCLUDE_DIRS}
)
target_link_directories(${PROJECT_NAME} BEFORE PRIVATE
  ${MRIOEXTRAS_LIBRARY_DIRS}
)
IF(MR_EMSCRIPTEN)
  target_link_libraries(${PROJECT_NAME} PRIVATE
    MRMesh
    spdlog
    tbb
    ${MRIOEXTRAS_DEPENDENCIES}
  )
ELSEIF(APPLE)
  target_link_libraries(${PROJECT_NAME} PRIVATE
    MRMesh
    fmt
    spdlog
    tbb
    ${MRIOEXTRAS_DEPENDENCIES}
  )
ELSE()
  target_link_libraries(${PROJECT_NAME} PRIVATE
    MRMesh
    fmt::fmt
    spdlog::spdlog
    tbb
    ${MRIOEXTRAS_DEPENDENCIES}
  )
ENDIF()

IF(WIN32 AND MINGW)
  target_compile_definitions(${PROJECT_NAME} PRIVATE MRIOEXTRAS_EXPORT)
ENDIF()

set(CMAKE_INSTALL_PREFIX "/Library/Frameworks")
install(
  TARGETS ${PROJECT_NAME}
  EXPORT ${PROJECT_NAME}
  LIBRARY DESTINATION "${MR_MAIN_LIB_DIR}"
  ARCHIVE DESTINATION "${MR_MAIN_LIB_DIR}"
  RUNTIME DESTINATION "${MR_BIN_DIR}"
)

install(
  FILES ${HEADERS}
  DESTINATION "${MR_INCLUDE_DIR}/${PROJECT_NAME}"
)

install(
  FILES ${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}Config.cmake
  DESTINATION ${MR_CONFIG_DIR}
)

install(
  EXPORT ${PROJECT_NAME}
  FILE ${PROJECT_NAME}Targets.cmake
  NAMESPACE MeshLib::
  DESTINATION ${MR_CONFIG_DIR}
)
